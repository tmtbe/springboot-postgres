import org.testcontainers.containers.MySQLContainer
buildscript {
    dependencies {
        classpath 'org.testcontainers:mysql:1.17.3'
        classpath 'mysql:mysql-connector-java:8.0.31'
        classpath 'org.flywaydb:flyway-mysql:9.0.1'
    }
}

plugins {
    id 'java'
    id 'nu.studer.jooq' version '8.2'
    id 'org.flywaydb.flyway' version '9.7.0'
}

configurations {
    flywayMigration
}

dependencies {
    flywayMigration 'mysql:mysql-connector-java:8.0.31'
    jooqGenerator 'mysql:mysql-connector-java:8.0.31'
    implementation 'mysql:mysql-connector-java:8.0.31'
    implementation 'org.flywaydb:flyway-core'
    implementation "org.flywaydb:flyway-mysql"
}

def dbContainerInfo = [
        jdbcUrl: '',
        username: '',
        password: '',
        databaseName: '',
        instance: null
]

class DBStartTask extends DefaultTask {
    @Input
    LinkedHashMap<String, String> containerInfo = null

    @TaskAction
    def start() {
        println("Starting MySQL container")
        var instance = new MySQLContainer("mysql:8.0.31")
                .withDatabaseName('example')
        instance.start()
        containerInfo.jdbcUrl = instance.getJdbcUrl()
        containerInfo.username = instance.getUsername()
        containerInfo.password = instance.getPassword()
        containerInfo.databaseName = instance.getDatabaseName()
        containerInfo.instance = instance
    }
}

task startDbContainer(type: DBStartTask) {
    containerInfo = dbContainerInfo
}

tasks.named('startDbContainer').configure {
    onlyIf {
        project.hasProperty("gen")
    }
}

tasks.named('flywayMigrate').configure {
    onlyIf {
        project.hasProperty("gen")
    }
    dependsOn tasks.named('startDbContainer')
    doFirst {
        println("Setting up flyway")
        flyway {
            configurations = ['flywayMigration']
            url = dbContainerInfo.jdbcUrl
            user = dbContainerInfo.username
            password = dbContainerInfo.password
        }
    }
}

jooq {
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                generator {
                    target {
                        packageName = "${group}.${rootProject.name}"
                    }
                }
            }
        }
    }
}

tasks.named('generateJooq').configure {
    onlyIf {
        project.hasProperty("gen")
    }
    dependsOn tasks.named('flywayMigrate')
    doFirst {
        jooq {
            configurations {
                main {
                    generationTool {
                        jdbc {
                            driver = 'com.mysql.cj.jdbc.Driver'
                            url = dbContainerInfo.jdbcUrl
                            user = dbContainerInfo.username
                            password = dbContainerInfo.password
                        }
                        generator {
                            name = 'org.jooq.codegen.DefaultGenerator'
                            database {
                                name = 'org.jooq.meta.mysql.MySQLDatabase'
                                includes = '.*'
                                inputSchema = dbContainerInfo.databaseName
                                outputSchemaToDefault = true
                            }
                            generate {
                                deprecated = false
                                records = true
                                pojos = true
                                interfaces = true
                                fluentSetters = true
                                nullableAnnotation = true
                                nullableAnnotationType = 'jakarta.annotation.Nullable'
                                nonnullAnnotation = true
                                nonnullAnnotationType = 'jakarta.annotation.Nonnull'
                                daos = true
                                springAnnotations = true
                                springDao = true
                            }
                        }
                    }
                }
            }
        }
    }
    doLast {
        println("Stopping MySQL container")
        dbContainerInfo.instance.stop()
    }
}